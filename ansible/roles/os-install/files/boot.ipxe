#!ipxe

set menu-timeout 30000
# HTTPS for Linux fetches (preseed, squashfs, config)
set server-url https://os-install.oneill.net
# HTTP for iPXE fetches (kernel, initrd) - bypasses Traefik which redirects to HTTPS
set insecure-server-url http://infra1.oneill.net:8081

menu PXE Boot Menu
item --gap -- -------- Default --------
item localdisk         Boot from local disk
item --gap -- -------- Debian Trixie --------
item trixie-preseed    Debian Trixie - Preseed
item trixie-expert     Debian Trixie - Expert
item --gap -- -------- Debian Bookworm --------
item bookworm-preseed  Debian Bookworm - Preseed
item bookworm-expert   Debian Bookworm - Expert
item --gap -- -------- Tools --------
item memtest           Memtest86+
item grml              Grml Rescue
item shell             iPXE Shell
choose --timeout ${menu-timeout} --default localdisk target && goto ${target}

:localdisk
exit

:trixie-preseed
set base http://deb.debian.org/debian/dists/trixie/main/installer-amd64/current/images/netboot/debian-installer/amd64
kernel ${base}/linux auto=true priority=critical preseed/url=${server-url}/preseed.trixie.cfg interface=auto
initrd ${base}/initrd.gz
boot

:trixie-expert
set base http://deb.debian.org/debian/dists/trixie/main/installer-amd64/current/images/netboot/debian-installer/amd64
kernel ${base}/linux priority=low
initrd ${base}/initrd.gz
boot

:bookworm-preseed
set base http://deb.debian.org/debian/dists/bookworm/main/installer-amd64/current/images/netboot/debian-installer/amd64
kernel ${base}/linux auto=true priority=critical preseed/url=${server-url}/preseed.bookworm.cfg interface=auto
initrd ${base}/initrd.gz
boot

:bookworm-expert
set base http://deb.debian.org/debian/dists/bookworm/main/installer-amd64/current/images/netboot/debian-installer/amd64
kernel ${base}/linux priority=low
initrd ${base}/initrd.gz
boot

:memtest
kernel ${insecure-server-url}/memtest64.bin
boot

:grml
kernel ${insecure-server-url}/grml/boot/grmlfullamd64/vmlinuz boot=live fetch=${server-url}/grml/live/grml-full-amd64/grml-full-amd64.squashfs netconfig=${server-url}/grml/config.tbz ssh=grml
initrd ${insecure-server-url}/grml/boot/grmlfullamd64/initrd.img
boot

:shell
shell
